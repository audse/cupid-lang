- file: expression+
-~expression: comment | statement | term

-~statement:
	type_definition
	| trait_definition
	| implement_type
	| implement_trait
	| typed_declaration
	| break
	| return
	| continue
	| op_assignment
	| op_increment_assignment
	| assignment
	| property_assignment
	| property_op_assignment
	| property_op_increment_assignment
	| log

-~term:
	loop
	| block
	| function
	| no_op
	| operation
	| atom

-~loop:
	for_loop
	| while_loop
	| infinite_loop

- for_loop: 'for' for_loop_parameters ~'in' term block
- for_loop_parameters: (identifier ~',')*
- while_loop: 'while' term block
- infinite_loop: 'loop' block

- block: 
	if_block
	| box_block
	| brace_block
	| arrow_block
	
- if_block: ~'if' term block else_if_block* else_block?
- else_if_block: ~'else' ~'if' term block
- else_block: ~'else' block
- box_block: ~'box' brace_block
-~brace_block: ~'{' expression* closing_brace
-~arrow_block: ~arrow expression

# separating this to allow arrays w/o brackets when assigning/declaring, but not when nested
-~assignment_value: array | require_term

- assignment: identifier equal assignment_value
- property_assignment: property_access equal assignment_value

- property_op_assignment: property_access operator equal assignment_value
- property_op_increment_assignment: property_access increment_operator

- op_assignment: identifier operator equal assignment_value
-~operator: '+' | '-' | '*' | '/' | '^' | '%'
- op_increment_assignment: identifier increment_operator
-~increment_operator: '+' '+' | '-' '-'

-~type_definition:
	builtin_type_definition
	| struct_type_definition
	| sum_type_definition
	| alias_type_definition

- builtin_type_definition: 'type' WORD !'='

- struct_type_definition: type_symbol ~equal '[' (struct_member ~',')* ~']'
- struct_member: type_hint identifier

- sum_type_definition: type_symbol ~equal '[' (sum_member ~',')* ~']'
- sum_member: type_hint

- alias_type_definition: type_symbol ~equal type_hint

-~type_symbol: 'type' generics? identifier
- generics: '[' (identifier ',')* closing_bracket

- typed_declaration: type_hint 'mut'? identifier (~equal assignment_value)?

-~type_hint:
	array_type_hint
	| function_type_hint
	| map_type_hint
	| struct_type_hint
	| primitive_type_hint

- array_type_hint: 'array' '[' type_hint closing_bracket
- map_type_hint: 'map' '[' type_hint ',' type_hint closing_bracket
- function_type_hint: 'fun' '[' type_hint closing_bracket
- primitive_type_hint: identifier

- struct_type_hint: identifier '[' (struct_member_type_hint ',')* ']'
- struct_member_type_hint: identifier ':' type_hint

- implement_type: 'use' generics? WORD '{' typed_declaration* closing_brace

- implement_trait: 'use' generics? identifier 'with' identifier implement_trait_body?
-~implement_trait_body: '{' typed_declaration* closing_brace

- trait_definition: 'trait' generics identifier equal '[' (typed_declaration ',')* ']'

-~equal: '=' !'>'

-~atom:
	function_call
	| map
	| bracket_array
	| group
	| unary_op
	| type_hint
	| boolean
	| none
	| string
	| char
	| decimal
	| number
	| identifier

- group: ~'(' term closing_paren
- function: parameters block
- parameters: (parameter ~',')*
-~parameter: ~'_' | ~'self' | annotated_parameter
- annotated_parameter: type_hint identifier

- log: log_keyword ~'(' arguments closing_paren
- function_call: identifier ~'(' arguments closing_paren
- arguments: (term ~',')*

-~log_keyword: 'log' | 'logs' | 'log_line' | 'logs_line'

-~bracket_array: '[' array? closing_bracket
- array:  (operation ~',')+

- map: '[' (map_entry !']' ',')* ']'
- map_entry: atom ':' term

- range: 
	range_inclusive_inclusive
	| range_inclusive_exclusive
	| range_exclusive_inclusive
	| range_exclusive_exclusive

- range_inclusive_inclusive: '[' atom ~'.' ~'.' atom ']'
- range_inclusive_exclusive: '[' atom ~'.' ~'.' ']' atom
- range_exclusive_inclusive: atom '[' ~'.' ~'.' atom ']'
- range_exclusive_exclusive: atom '[' ~'.' ~'.' ']' atom

# operations can be kinda slow, going through all that, so this is a shortcut
-~no_op: !'+' !'-' atom !operator !keyword_operator !'.'

-~operation: binary_op

-~binary_op: compare_op

- compare_op: add compare_suffix?
-~compare_suffix: keyword_operator compare_op

- add: multiply add_suffix?
-~add_suffix: '+' add | '-' add

- multiply: exponent multiply_suffix?
-~multiply_suffix: '*' multiply | '/' multiply | '%' multiply

- exponent: property_access exponent_suffix?
-~exponent_suffix: '^' exponent

- property_access: atom property_access_suffix?
-~property_access_suffix: '.' property_access

- unary_op: '+' atom | '-' atom

- break: 'break' term?
- return: 'return' term?
- continue: 'continue'

- boolean: 'true' | 'false'
- none: 'none'
- identifier: !keyword WORD
- char: '\\' LETTER 
- string: STRING
- decimal: NUMBER ~'.' NUMBER
- number: NUMBER

# Error handling
-~require_term: term | <e 'missing expression'>
-~closing_paren: ~')' | <e 'missing closing parenthesis'>
-~closing_brace: ~'}' | <e 'missing closing brace'>
-~closing_bracket: ~']' | <e 'missing closing bracket'>

-~keyword:
	keyword_variable
	| keyword_operator
	| reserved_word
	| boolean
	| none

-~reserved_word:
	'for'
	| 'while'
	| 'else'
	| 'if'
	| 'mut'
	| 'loop'
	| 'box'
	| 'break'
	| 'return'
	| 'continue'
	| 'type'
	| 'log'
	| 'logs'
	| 'log_line'
	| 'logs_line'
	| 'use'
	| 'trait'

-~keyword_variable:
	'let'
	| 'const'

-~keyword_operator:
	'in'
	| 'is'
	| 'and'
	| 'not'
	| 'or'
	| 'as'
	| 'istype'
	| '>'
	| '>' '='
	| '<'
	| '<' '='

- arrow: ~'=' ~'>'
	
-~comment_delimiter: '*' '*' '*'
-~comment_content: ANY !comment_delimiter
- comment: ~comment_delimiter comment_content* ANY ~comment_delimiter