- file: expression+
- expression: expression_item | comment

-~expression_item:
	loop
	| block
	| type_definition
	| declaration
	| symbol_declaration
	| typed_declaration
	| function
	| internal_property_assignment
	| property_assignment
	| assignment
	| term

-~loop:
	for_loop
	| while_loop
	| infinite_loop

- for_loop: 'for' parameters ~'in' term block
- while_loop: 'while' expression_item block
- infinite_loop: 'loop' block

- block: 
	if_block
	| brace_block
	| arrow_block
	
- if_block: ~'if' expression block else_if_block* else_block?
- else_if_block: ~'else' ~'if' expression block
- else_block: ~'else' block
-~brace_block: ~'{' expression* closing_brace
-~arrow_block: ~arrow require_expression

- declaration: symbol_declaration ~equal expression_item
- symbol_declaration: keyword_variable 'mut'? identifier
- assignment: identifier equal expression
- internal_property_assignment: internal_property_access equal expression
- property_assignment: property_access equal expression

- type_definition: 'type' identifier ~'[' (type_field ',')* closing_bracket
- type_field: type identifier | identifier identifier

- typed_declaration: WORD 'mut'? identifier (~equal expression_item)?

-~equal: '=' !'>'

-~term:
	log
	| structure
	| operation

-~structure:
	dictionary
	| list

-~value:
	group
	| function_call
	| internal_property_access
	| property_access
	| boolean
	| none
	| string
	| decimal
	| number
	| identifier

- group: ~'(' expression_item closing_paren
- function: parameters block
- parameters: (identifier ~',')*

- log: log_keyword ~'(' arguments closing_paren
- function_call: identifier ~'(' arguments closing_paren
- arguments: (expression_item ~',')*

-~log_keyword: 'log' | 'logs' | 'log_line' | 'logs_line'

- list: '[' (expression_item !']' ',')* ']'
- dictionary: '[' (dictionary_entry !']' ',')* ']'
- dictionary_entry: term ':' expression_item

- internal_property_access: 'self' '.' require_property
- property_access: property_accessor '.' require_property
-~property_accessor: structure | function_call | identifier | 'self'
-~require_property: value | <e 'missing index or property name'>

-~operation:
	binary_op
	| unary_op

-~binary_op: compare_op

- compare_op: add compare_suffix?
-~compare_suffix: keyword_operator compare_op

- add: multiply add_suffix?
-~add_suffix: '+' add | '-' add

- multiply: exponent multiply_suffix?
-~multiply_suffix: '*' multiply | '/' multiply

- exponent: value exponent_suffix?
-~exponent_suffix: '^' exponent

- unary_op: unary_operator require_expression
-~unary_operator: '+' | '-'

- boolean: 'true' | 'false'
- none: 'none'
- identifier: !keyword WORD
- string: STRING
- decimal: NUMBER ~'.' NUMBER
- number: NUMBER

# Error handling
-~require_expression: expression_item | <e 'missing expression'>
-~closing_paren: ~')' | <e 'missing closing parenthesis'>
-~closing_brace: ~'}' | <e 'missing closing brace'>
-~closing_bracket: ~']' | <e 'missing closing bracket'>

-~keyword:
	keyword_variable
	| keyword_operator
	| keyword_control
	| type
	| boolean
	| none
	| 'for'
	| 'while'
	| 'else'
	| 'if'
	| 'loop'

-~keyword_variable:
	'let'
	| 'const'

-~keyword_operator:
	'in'
	| 'is'
	| 'and'
	| 'not'
	| 'or'
	| '>'
	| '>' '='
	| '<'
	| '<' '='

-~keyword_control:
	'break'
	| 'return'

- arrow: ~'=' ~'>'

- type:
	none
	| 'bool'
	| 'int'
	| 'dec'
	| 'string'
	| 'fun'
	| 'maybe'
	| 'list'
	| 'dict'
	| 'tuple'
	
-~comment_delimiter: '*' '*' '*'
-~comment_content: ANY !comment_delimiter
- comment: ~comment_delimiter comment_content* ANY ~comment_delimiter