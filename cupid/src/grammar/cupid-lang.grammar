- file: expression+
- expression: comment | statement | expression_item

-~statement:
	type_definition
	| type_alias_definition
	| typed_declaration

-~expression_item:
	loop
	| block
	| break
	| return
	| continue
	| function
	| internal_property_assignment
	| property_assignment
	| op_assignment
	| op_increment_assignment
	| assignment
	| array
	| term

-~loop:
	for_loop
	| while_loop
	| infinite_loop

- for_loop: 'for' for_loop_parameters ~'in' term block
- for_loop_parameters: (identifier ~',')*
- while_loop: 'while' expression_item block
- infinite_loop: 'loop' block

- block: 
	if_block
	| box_block
	| brace_block
	| arrow_block
	
- if_block: ~'if' expression block else_if_block* else_block?
- else_if_block: ~'else' ~'if' expression block
- else_block: ~'else' block
- box_block: ~'box' brace_block
-~brace_block: ~'{' expression* closing_brace
-~arrow_block: ~arrow require_expression

- assignment: identifier equal expression
- internal_property_assignment: internal_property_access equal expression
- property_assignment: property_access equal expression

- op_assignment: identifier operator equal expression
-~operator: '+' | '-' | '*' | '/' | '^' | '%'
- op_increment_assignment: identifier '+' '+' | identifier '-' '-'

- type_definition: 'type' identifier equal ~'[' (type_field ',')* closing_bracket
- type_alias_definition: 'type' identifier equal type_hint
- type_field: type_hint identifier

- typed_declaration: type_hint 'mut'? identifier (~equal expression_item)?

- type_hint: type_id type_hint_details?
-~type_hint_details: '('  (type_hint ',')* ')'
-~type_id: type | identifier

-~equal: '=' !'>'

-~term:
	log
	| structure
	| operation

-~structure:
	dictionary
	| list
	| range

-~value:
	function_call
	| internal_property_access
	| property_access
	| unary_op
	| atom

-~atom:
	group
	| boolean
	| none
	| string
	| char
	| decimal
	| number
	| identifier

- group: ~'(' expression_item closing_paren
- function: parameters block
- parameters:  ~'_' | (annotated_parameter ~',')*
- annotated_parameter: type_hint identifier

- log: log_keyword ~'(' arguments closing_paren
- function_call: identifier ~'(' arguments closing_paren
- arguments: (expression_item ~',')*

-~log_keyword: 'log' | 'logs' | 'log_line' | 'logs_line'

- array: (term ~',')+
- list: '[' (expression_item !']' ',')* ']'
- dictionary: '[' (dictionary_entry !']' ',')* ']'
- dictionary_entry: term ':' expression_item

- range: 
	range_inclusive_inclusive
	| range_inclusive_exclusive
	| range_exclusive_inclusive
	| range_exclusive_exclusive

- range_inclusive_inclusive: '[' atom ~'.' ~'.' atom ']'
- range_inclusive_exclusive: '[' atom ~'.' ~'.' ']' atom
- range_exclusive_inclusive: atom '[' ~'.' ~'.' atom ']'
- range_exclusive_exclusive: atom '[' ~'.' ~'.' ']' atom

- internal_property_access: 'self' '.' require_property
- property_access: property_accessor '.' require_property
-~property_accessor: structure | function_call | identifier | 'self'
-~require_property: value | <e 'missing index or property name'>

-~operation: binary_op

-~binary_op: compare_op

- compare_op: add compare_suffix?
-~compare_suffix: keyword_operator compare_op

- add: multiply add_suffix?
-~add_suffix: '+' add | '-' add

- multiply: exponent multiply_suffix?
-~multiply_suffix: '*' multiply | '/' multiply | '%' multiply

- exponent: value exponent_suffix?
-~exponent_suffix: '^' exponent

- unary_op: '+' value | '-' value

- break: 'break' expression_item?
- return: 'return' expression_item?
- continue: 'continue'

- boolean: 'true' | 'false'
- none: 'none'
- identifier: !keyword WORD
- char: '\\' LETTER 
- string: STRING
- decimal: NUMBER ~'.' NUMBER
- number: NUMBER

# Error handling
-~require_expression: expression_item | <e 'missing expression'>
-~closing_paren: ~')' | <e 'missing closing parenthesis'>
-~closing_brace: ~'}' | <e 'missing closing brace'>
-~closing_bracket: ~']' | <e 'missing closing bracket'>

-~keyword:
	keyword_variable
	| keyword_operator
	| reserved_word
	| type
	| boolean
	| none

-~reserved_word:
	'for'
	| 'while'
	| 'else'
	| 'if'
	| 'loop'
	| 'box'
	| 'break'
	| 'return'

-~keyword_variable:
	'let'
	| 'const'

-~keyword_operator:
	'in'
	| 'is'
	| 'and'
	| 'not'
	| 'or'
	| '>'
	| '>' '='
	| '<'
	| '<' '='

- arrow: ~'=' ~'>'

- type:
	none
	| 'bool'
	| 'int'
	| 'dec'
	| 'string'
	| 'fun'
	| 'maybe'
	| 'list'
	| 'dict'
	| 'tuple'
	| 'char'
	| 'array'
	
-~comment_delimiter: '*' '*' '*'
-~comment_content: ANY !comment_delimiter
- comment: ~comment_delimiter comment_content* ANY ~comment_delimiter